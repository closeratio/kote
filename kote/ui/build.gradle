/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	ext {
		kotlinxHtmlVersion = '0.6.10'

		reactVersion = "16.6.0-pre.67"
		reactDomVersion = "16.6.0-pre.67"
		styledVersion = "1.0.0-pre.67"

		muirwikVersion = '0.2.1'
	}

	repositories {
		jcenter()
		maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
	}
}

plugins {
	id "kotlin2js"
	id "com.moowork.node" version "1.2.0"
}

apply plugin: 'org.jetbrains.kotlin.frontend'

sourceCompatibility = 1.8

repositories {
	jcenter()
	maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
	maven { url "http://dl.bintray.com/kotlin/kotlin-js-wrappers" }
}

apply from: 'dependencies.gradle'

kotlinFrontend {
	npm {
		dependency "style-loader" // production dependency
		dependency "react"
		dependency "react-dom"
		dependency "kotlin"
		dependency "inline-style-prefixer"
		dependency "kotlinx-html-js"
		dependency "kotlin-react"
		dependency "kotlin-react-dom"
		dependency "kotlin-styled"
		dependency "muirwik-components"
	}

	webpackBundle {
		bundleName = "main"
		sourceMapEnabled = true   // enable/disable source maps
		contentPath = file("src/main/web") // a file that represents a directory to be served by dev server)
		publicPath = "/"  // web prefix
		host = "localhost" // dev server host
		port = 8088   // dev server port
		stats = "errors-only"  // log level
	}
}

compileKotlin2Js {
	kotlinOptions {
		outputFile = "$project.buildDir.path/js/${project.name}.js"
		moduleKind = "commonjs"
		sourceMap = true
		metaInfo = true
		main = "call"
	}
}
